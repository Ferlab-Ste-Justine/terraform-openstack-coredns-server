variable "name" {
  description = "Name to give to the vm"
  type        = string
  default     = "etcd"
}

variable "image_source" {
  description = "Source of the vm's image"
  type = object({
    image_id  = string
    volume_id = string
  })
}

variable "flavor_id" {
  description = "ID of the flavor the etcd instance will run on"
  type        = string
}

variable "network_port" {
  description = "Network port to assign to the node. Should be of type openstack_networking_port_v2"
  type        = any
}

variable "server_group" {
  description = "Server group to assign to the node. Should be of type openstack_compute_servergroup_v2"
  type        = any
}

variable "keypair_name" {
  description = "Name of the keypair that will be used to ssh to the etcd instance"
  type        = string
}

variable "etcd" {
  description = "Parameters for the etcd backend"
  type = object({
    key_prefix     = string
    endpoints      = list(string)
    ca_certificate = string
    client = object({
      certificate = string
      key         = string
      username    = string
      password    = string
    })
  })
}

variable "dns" {
  description = "Parameters for the DNS server"
  type = object({
    zonefiles_reload_interval = string
    load_balance_records      = bool
    alternate_dns_servers     = list(string)
    forwards = list(object({
      domain_name = string
      dns_servers = list(string)
    }))
    cache_settings = object({
      domains  = list(string)
      max_ttl  = number  
      prefetch = object({ 
        amount   = number    
        duration = string 
      })
    })
  })
  default = {
    zonefiles_reload_interval = "3s"
    load_balance_records      = true
    alternate_dns_servers     = []
    forwards                  = []
    cache_settings = {
      domains  = []         
      max_ttl  = 86400    
      prefetch = {                    
        amount   = 0                 
        duration = ""    
      }
    }
  }
}


variable "chrony" {
  description = "Chrony configuration for ntp. If enabled, chrony is installed and configured, else the default image ntp settings are kept"
  type = object({
    enabled = bool,
    //https://chrony.tuxfamily.org/doc/4.2/chrony.conf.html#server
    servers = list(object({
      url     = string,
      options = list(string)
    })),
    //https://chrony.tuxfamily.org/doc/4.2/chrony.conf.html#pool
    pools = list(object({
      url     = string,
      options = list(string)
    })),
    //https://chrony.tuxfamily.org/doc/4.2/chrony.conf.html#makestep
    makestep = object({
      threshold = number
      limit     = number
    })
  })
  default = {
    enabled = false
    servers = []
    pools   = []
    makestep = {
      threshold = 0
      limit     = 0
    }
  }
}

variable "fluentbit" {
  description = "Fluent-bit configuration"
  type = object({
    enabled             = bool
    coredns_tag         = string
    coredns_updater_tag = string
    node_exporter_tag   = string
    metrics = object({
      enabled = bool
      port    = number
    })
    forward = object({
      domain     = string
      port       = number
      hostname   = string
      shared_key = string
      ca_cert    = string
    })
  })
  default = {
    enabled             = false
    coredns_tag         = ""
    coredns_updater_tag = ""
    node_exporter_tag   = ""
    metrics = {
      enabled = false
      port    = 0
    }
    forward = {
      domain     = ""
      port       = 0
      hostname   = ""
      shared_key = ""
      ca_cert    = ""
    }
  }
}

variable "fluentbit_dynamic_config" {
  description = "Parameters for fluent-bit dynamic config if it is enabled"
  type = object({
    enabled = bool
    source  = string
    etcd = object({
      key_prefix     = string
      endpoints      = list(string)
      ca_certificate = string
      client = object({
        certificate = string
        key         = string
        username    = string
        password    = string
      })
    })
    git = object({
      repo             = string
      ref              = string
      path             = string
      trusted_gpg_keys = list(string)
      auth = object({
        client_ssh_key         = string
        server_ssh_fingerprint = string
      })
    })
  })
  default = {
    enabled = false
    source  = "etcd"
    etcd = {
      key_prefix     = ""
      endpoints      = []
      ca_certificate = ""
      client = {
        certificate = ""
        key         = ""
        username    = ""
        password    = ""
      }
    }
    git = {
      repo             = ""
      ref              = ""
      path             = ""
      trusted_gpg_keys = []
      auth = {
        client_ssh_key         = ""
        server_ssh_fingerprint = ""
      }
    }
  }

  validation {
    condition     = contains(["etcd", "git"], var.fluentbit_dynamic_config.source)
    error_message = "fluentbit_dynamic_config.source must be 'etcd' or 'git'."
  }
}

variable "install_dependencies" {
  description = "Whether to install all dependencies in cloud-init"
  type        = bool
  default     = true
}